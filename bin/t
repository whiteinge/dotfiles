#!/usr/bin/env zsh
# Store a todo list in any Git repository
#
# A semi-port of the excellent t to use Git as storage.
# https://github.com/sjl/t
#
# The todos are stored as namespaced refs. The namespace default namespace can
# be overridden by setting TODO_NS.
#
# Setup: either the CWD should be a Git repo or the TODO_REPO variable should
# point to a Git repo used to store the todo list.

NAME=$(basename "$0")
TODO_REPO=${TODO_REPO:-'./.git'}
TODO_NS=${TODO_NS:-'todo'}

local -a git=( "git" "--git-dir" "${TODO_REPO}" )

add() {
    local in="$*"

    if [[ $in == *$'\n'* ]]; then
        printf 'TODOs cannot be multi-line.\n' 1>&2
        exit 1
    fi

    printf '%s' "$in" \
        | "$git[@]" hash-object --stdin -w \
        | xargs -I{} "$git[@]" update-ref "refs/${TODO_NS}/{}" {}
}

list() {
    "${git[@]}" for-each-ref --format '%(refname:lstrip=2)' "refs/${TODO_NS}" \
        | "${git[@]}" cat-file --batch='%(objectname)' \
        | xargs -r -d '\n' -n 2 \
        | while read -r sha content; do
            printf '%s - %s\n' \
                "$(${git[@]} rev-parse --short=4 ${sha})" \
                "$content"
        done \
        | sort
}

del() {
    local id="${1:?Todo ID is required.}"
    "$git[@]" rev-parse "$id" \
        | xargs -r -I{} "$git[@]" update-ref -d "refs/${TODO_NS}/{}" {}
}

help() {
    cat <<-EOF
	Usage: ${NAME} [<flags>] [*<arg>]

	List all todos: ${NAME}

	Add a new todo: ${NAME} Walk the plants.
	              : ${NAME} Water the dog.

	Flag        | Description
	----------- | -----------
	-d <hash>   | Show version.
	-h          | Show this screen.
	-x          | Enable xtrace debug logging.

	Flags _must_ be the first argument to ${NAME}.
EOF
}

main() {
    local cmd
    local opt
    local OPTARG
    local OPTIND

    test -d "$TODO_REPO"
    if [ "$?" -ne 0 ]; then
        printf 'Todo repository "%s" not found.\n' "$TODO_REPO" 1>&2
        exit 1
    fi

    command -v "git" 1>/dev/null 2>/dev/null
    if [ "$?" -ne 0 ]; then
        printf 'Git must be installed.\n' 1>&2
        exit 1
    fi

    if [ $# -eq 0 ]; then
        cmd='list'
    else
        while getopts hd opt; do
            case $opt in
                h) cmd='help';;
                d) cmd='del';;
            esac
        done
        shift $(( OPTIND - 1 ))

        cmd="${cmd:-add}"
    fi

    "$cmd" "$@"
}

main "$@"
